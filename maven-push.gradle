apply plugin: 'maven'

def mavenRepositoryUrl

if (isReleaseBuild())
{
    println 'RELEASE BUILD'
    mavenRepositoryUrl = "http://maven.livotovlabs.pro/content/repositories/releases/"
} else
{
    println 'SNAPSHOT BUILD'
    mavenRepositoryUrl = "http://maven.livotovlabs.pro/content/repositories/snapshots/"
}

def getRepositoryUsername()
{
    return hasProperty('mavenRepoUsername') ? mavenRepoUsername : ""
}

def getRepositoryPassword()
{
    return hasProperty('mavenRepoPassword') ? mavenRepoPassword : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: mavenRepositoryUrl) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                addFilter('addon') { artifact, file ->
                    artifact.name == 'addon'
                }

                pom('addon').artifactId = POM_ARTIFACT_ID

                pom('addon').project {
                    name POM_NAME
                    packaging POM_PACKAGING
                    description POM_DESCRIPTION
                    url POM_URL

                    scm {
                        url POM_SCM_URL
                        connection POM_SCM_CONNECTION
                        developerConnection POM_SCM_DEV_CONNECTION
                    }

                    licenses {
                        license {
                            name POM_LICENCE_NAME
                            url POM_LICENCE_URL
                            distribution POM_LICENCE_DIST
                        }
                    }

                    developers {
                        developer {
                            id POM_DEVELOPER_ID
                            name POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
    }

    artifacts {
        archives file: file('build/libs/tpt-addon-' + VERSION_NAME + '.jar'), name: 'addon', type: 'jar'
    }
}
